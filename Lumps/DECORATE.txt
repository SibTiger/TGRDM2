// ===============================================
//              SkullTag Resources
// ===============================================
// Credits go to the SkullTag Team

actor DeadCyberdemon : Cyberdemon 5070
{
	Skip_Super
	Game Doom
	DropItem None
	States
	{
	Spawn:
		Goto Super::Death+9
	}
}

ACTOR GreyHeartColumn 5053
{
	Game Doom
	Game Heretic
	Game Hexen
	Radius 16
	Height 40
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
		COL9 AB 14
		Loop
	}
}

ACTOR GreyColumn 5051
{
	Game Doom
	Game Heretic
	Game Hexen
	Radius 16
	Height 40
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
		COL8 A -1
		Stop
	}
}

// ===============================================
//              REALM667 Resources
// ===============================================

//BFG10k
//Credits: The SkullTag Team, Eriance, and Ghastly_Dragon.
Actor STBFG10K : DoomWeapon 5013
{
  Radius 20
  Height 20
  Weapon.Selectionorder 2800
  Weapon.SlotNumber 7
  Weapon.AmmoUse 5
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Weapon.Kickback 100
  Inventory.Pickupmessage "You got the BFG10000!  Hell, yes!"
  Obituary "%o was blasted by %k's BFG10K"
  +Weapon.NoAutoFire
  Decal BFGLightning
  States
  {
  Spawn:
    BFG2 A -1
    Stop
  Ready:
    BG2G A 0 A_PlaySound ("Weapons/BFG10KIdle")
    BG2G A 1 A_WeaponReady
    BG2G A 1 A_WeaponReady
    BG2G A 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    Loop
  Deselect:
    BG2G E 1 A_Lower
    Loop
  Select:
    BG2G E 1 A_Raise
    Loop
  Fire:
    BG2G E 20 A_PlaySound("Weapons/BFG10KF")
    BG2G F 4
    BG2G G 1
    BG2G H 1
    BG2G I 1
    BG2G J 1
    Goto Hold
  Hold:
    BG2G K 2 A_GunFlash
    BG2G L 2 A_FireBullets(0, 0, 1, 0, "STBFG10kShot")
    BG2G M 2
    BG2G N 0 A_ReFire
    BG2G O 35 A_PlaySound("Weapons/BFG10KCool")
    Goto Ready
  Flash:
    TNT1 A 2 Bright A_Light1
    TNT1 A 3 bright
    Goto LightDone
  }
}


Actor STBFG10kShot
{
  Radius 11
  Height 8
  Speed 20
  Damage 160
  DamageType BFG10K
  +NoBlockMap
  +NoGravity
  +ActivateImpact
  +ActivatePCross
  +NoTeleport
  +PuffOnActors
  +PuffGetsOwner
  +ForceRadiusDmg
  Renderstyle Add
  Alpha 0.75
  SeeSound "Weapons/BFG10KX"
  AttackSound "Weapons/BFG10KX"
  Obituary "%o was blasted by %k's BFG10K"
  States
  {
  Spawn:
    BFE1 A 0 Bright
    BFE1 A 3 Bright A_Explode(160, 128, 1)
    BFE1 BCDEF 3 Bright
    Stop
  }
}

//GrenadeLauncher
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor STGrenadeLauncher : Weapon 5011
{
  Radius 20
  Height 16
  Weapon.SelectionOrder 2500
  Weapon.SlotNumber 5
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOAUTOAIM
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  Weapon.Kickback 100
  Inventory.PickupMessage "You got the grenade launcher!"
  Obituary "%o caught %k's grenade."
  States
  {
  Spawn:
    GLAU A -1
    Stop
  Ready:
    GRLG A 1 A_WeaponReady
    Loop
  Deselect:
    GRLG A 1 A_Lower
    Loop
  Select: 
    GRLG A 1 A_Raise
    Loop
  Fire: 
    GRLG B 8 A_GunFlash
    GRLG B 12 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
    GRLG B 0 A_ReFire
    Goto Ready 
  Flash: 
    GRLF A 3 Bright A_Light1
    GRLF B 4 Bright
    GRLF C 4 Bright A_Light2
    GRLF D 4 Bright A_Light2
    Goto LightDone
  }
}

Actor STGrenade
{
  Radius 8
  Height 8
  Speed 25
  Damage 20
  Gravity 0.25
  Projectile
  -NoGravity
  +DoomBounce
  +DehExplosion
  +GrenadeTrail
  +ExplodeOnDeath
  SeeSound "Weapons/GrBnce"
  DeathSound "Weapons/GrenLX"
  Obituary "%o caught %k's grenade."
  DamageType Fire
  States
  {
  Spawn:
    SGRN A 0 Bright
    SGRN A 0 Bright A_StopSound
    SGRN A 0 Bright A_PlaySound("Weapons/GrenLF")
    SGRN A 1 Bright 
    Goto Spawn+3
  Death:
    MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    stop
  }
}

//Minigun
//Credits: The SkullTag Team, and Ghastly_Dragon.
ACTOR STMinigun : Weapon 5014
{
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Weapon.SlotNumber 4
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Picked up a minigun!"
	Obituary "%o was shredded by %k's minigun."
	AttackSound "weapons/minigunfire"
	States
	{
		Spawn:
		MNGN A -1
		Loop
		Ready:
		MNGG A 1 A_WeaponReady
		Loop
		Deselect:
		MNGG A 1 A_Lower
		Loop
		Select:
		MNGG A 1 A_Raise
		Loop
		Fire:
		MNGG A 0 A_GunFlash
		MNGG A 2 A_FireBullets(5, 1, 1, 5)
		MNGG B 2 A_FireBullets(5, 1, 1, 5)
		MNGG B 0 A_ReFire
		MNGG B 2
		MNGG A 5
		MNGG B 8
		MNGG A 11
		MNGG B 14
		MNGG A 17
		goto Ready
		Flash:
		MNGF A 3 BRIGHT A_Light1
		MNGF B 3 BRIGHT A_Light0
		Stop
	}
}

//Railgun
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor STRailgun : DoomWeapon 5012
{
  Radius 20
  Height 16
  Weapon.Selectionorder 100
  Weapon.AmmoUse 10
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Weapon.SlotNumber 6
  Inventory.Pickupmessage "You got the railgun!"
  Obituary "%o was railed by %k."
  Decal RailScorch
  States
  {
  Ready:
    RLGG A 1 A_WeaponReady
    Loop
  Deselect:
    RLGG A 1 A_Lower
    Loop
  Select:
    RLGG A 1 A_Raise
    Loop
  Fire:
    RLGG E 12 A_FireRailgun
    RLGG F 6 A_CheckForReload(4, "Reloaded")
    RLGG GHIJK 6
    RLGG L 6 A_ResetReloadCounter
  Reloaded:
    RLGG A 6
    RLGG M 0 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 5 bright A_Light1
    TNT1 A 5 bright A_Light2
    TNT1 A 0 bright A_Light0
    Goto LightDone
  Spawn:
    RAIL A -1
    Stop
  }
}

// DoomSphere
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor PowerQuadDamageST : PowerDamage
{
	Damagefactor "Normal", 4
}

Actor STDoomsphere : PowerupGiver 5036
{
  +CountItem
  +NoGravity
  +Inventory.AutoActivate
  +Inventory.AlwaysPickup
  Inventory.MaxAmount 0
  Powerup.Type QuadDamageST
  Powerup.Color RedMap
  Inventory.PickupMessage "Doomsphere"
  States
  {
  Spawn:
    DOOM A 10 Bright
    DOOM B 15 Bright
    DOOM C 8 Bright
    DOOM D 6 Bright
    Loop
  }
}

// TurboSphere
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor PowerTurboST : PowerSpeed
{
  Inventory.Icon "ARTITU1"
}

Actor TurbosphereST : PowerupGiver 5030
{
  +CountItem
  +NoGravity
  +Inventory.AutoActivate
  +Inventory.AlwaysPickup
  Inventory.MaxAmount 0
  Powerup.Type TurboST
  Powerup.Duration 1050
  Inventory.PickupMessage "Turbosphere!"
  States
  {
  Spawn:
    TURB ABCD 3 Bright
    Loop
  }
}

// InvisibilitySphere
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor InvisibilitySphereST : PowerupGiver 5035
{
  +CountItem
  +NoGravity
  +VisibilityPulse
  +Inventory.AutoActivate
  +Inventory.AlwaysPickup
  Inventory.MaxAmount 0
  Powerup.Type Invisibility
  Powerup.Mode Translucent
  Powerup.Strength 80
  RenderStyle Translucent
  Inventory.PickupMessage "Invisibility"
  States
  {
  Spawn:
    SINV ABCD 6 Bright
    Loop
  }
}

//Completely abolish the runes from the maps without having to physically alter
// the maps.
Actor RemoveSkullTagRuneStrength 5100
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneRage 5101
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneRegeneration 5105
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneSpread 5103
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

// Link these ST bonuses to Doom's standard bonus items
Actor SkullTagArmorBonus : ArmorBonus 5091
{
	//This is only going to inherit Doom's ArmorBonus.
	//This is only a link to another Actor.
}

Actor SkullTagHealthBonus : HealthBonus 5090
{
	//This is only going to inherit Doom's HealthBonus.
	//This is only a link to another Actor.
}

Actor SkullTagRedArmor : BlueArmor 5040
{
	//This is only going to inherit Doom's BlueArmor
	//This is only a link to another Actor.
}

// Link the RandSphere to SoulSphere
Actor SkullTagRandSphere : SoulSphere 5039
{
	//This is only going to inherit Doom's SoulSphere
	//This is only a link to another Actor.
}